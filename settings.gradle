/*
 * Copyright 2016-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
	plugins {
		id 'io.spring.nohttp' version '0.0.11'
		id 'io.spring.javaformat' version '0.0.41'
		id 'org.asciidoctor.jvm.convert' version '3.3.2'
		id 'org.springframework.boot' version '3.1.10'
	}
	repositories {
		gradlePluginPortal()
		maven { url "https://repo.spring.io/plugins-release" }
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/plugins-snapshot" }
		}
	}
}

plugins {
	id 'com.gradle.enterprise' version '3.14.1'
	id 'io.spring.ge.conventions' version '0.0.15'
}

dependencyResolutionManagement {
	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/release' }
	}
	if (version =~ /((-M|-RC)[0-9]+|-SNAPSHOT)$/) {
		repositories {
			maven { url 'https://repo.spring.io/milestone' }
		}
	}
	if (version.endsWith('-SNAPSHOT')) {
		repositories {
			maven { url 'https://repo.spring.io/snapshot' }
		}
	}
}

rootProject.name = 'spring-credhub'

include ':spring-credhub-core'
include ':spring-credhub-starter'
include ':spring-credhub-integration-tests'
include ':spring-credhub-reactive-integration-tests'
include ':spring-credhub-docs'
